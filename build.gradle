plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version + "-" + project.minecraft_version
group = project.maven_group

loom {
    accessWidenerPath = file("src/main/resources/identity.accesswidener")
}

repositories {
    maven { url 'https://jitpack.io' }

    maven {
        name = 'Ladysnake Mods'
        url = 'https://ladysnake.jfrog.io/artifactory/mods'
        content {
            includeGroup 'io.github.ladysnake'
            includeGroupByRegex 'io\\.github\\.onyxstudios.*'
        }
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    // Player Ability Lib: https://github.com/Ladysnake/PlayerAbilityLib
    include "io.github.ladysnake:PlayerAbilityLib:${project.pal_version}"
    modImplementation ("io.github.ladysnake:PlayerAbilityLib:${project.pal_version}") {
        exclude group: "net.fabricmc.fabric-api"
    }

    // Cardinal Components: https://github.com/OnyxStudios/Cardinal-Components-API
    modImplementation "io.github.onyxstudios:Cardinal-Components-API:${project.cardinal_version}"
    include "io.github.onyxstudios:Cardinal-Components-API:${project.cardinal_version}"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-base:${project.cardinal_version}"
    include "io.github.onyxstudios.Cardinal-Components-API:cardinal-components-entity:${project.cardinal_version}"

    // config
    modImplementation 'com.github.Draylar.omega-config:omega-config-base:1.0.4-beta'
    include 'com.github.Draylar.omega-config:omega-config-base:1.0.4-beta:min'
}

processResources {
    inputs.property "version", project.version

    filesMatching('fabric.mod.json') {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    // Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
    it.options.release = 17
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }

    // See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
    repositories {
        // Add repositories to publish to here.
        // Notice: This block does NOT have the same function as the block in the top level.
        // The repositories here will be used for publishing your artifact, not for
        // retrieving dependencies.
    }
}
